buildscript {
    dependencies {
        classpath "net.serenity-bdd:serenity-gradle-plugin:2.1.4"
        classpath "net.serenity-bdd:serenity-single-page-report:2.1.4"
    }
}

plugins {
    id "info.solidsoft.pitest" version '1.5.1'
}
description = 'api module'

apply from: 'liquibase.gradle'
apply plugin: 'net.serenity-bdd.aggregator'

sourceSets {

    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
    }
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
    }
}

apply plugin: 'idea'
idea {
    module {
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    }
}

def versions = [
        pitest: '1.5.1',
        gradlePitest: '1.3.0',
        sonarPitest: '0.5',
        serenity                : '2.2.12',
        serenityreporter        : '2.1.4',
        serenityCucumber        : '1.9.51'
]

sonarqube {
    properties {
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

def javaLoggingVersion = '5.1.1'
dependencies {
    compile project(':fees-register-model')
    compile project(':fees-register-api-contract')
    compile (group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.2') {
        exclude(module: 'java-logging-spring')
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile (group: 'org.postgresql', name: 'postgresql', version: '42.2.13') {
        exclude(module: 'commons-logging')
        exclude(module: 'slf4j-simple')
    }
    compile group: 'org.liquibase', name: 'liquibase-core', version: '4.2.1'
    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: javaLoggingVersion
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: javaLoggingVersion
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.0'
    runtime group: 'org.springframework.boot', name: 'spring-boot-devtools'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '5.2.1.RELEASE'
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.5'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
    testCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.12'


    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'


    testCompile 'io.github.openfeign:feign-jackson:9.7.0'
    // smoke
    smokeTestCompile sourceSets.test.runtimeClasspath
    // functional
    functionalTestCompile sourceSets.test.runtimeClasspath
    functionalTestCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    functionalTestCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    functionalTestCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    functionalTestCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    functionalTestCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    compile group: 'org.pitest', name: 'pitest', version: versions.pitest
    compile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6'
    compile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
}

test {
    // Base test class for other tests - has no runnable test methods
    exclude '**/ComponentTestBase*'
}

task smokeTest(type: Test, description: 'Runs the smoke tests', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

serenity {
    reports = ["single-page-html"]
}

task functionalTest(type: Test, description: 'Runs the smoke functional', group: 'Verification') {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

functionalTest.finalizedBy(aggregate)

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
    pacticipant = 'feesregister_App'
    pacticipantVersion = getCheckedOutGitCommitHash()
}


task consumerContractTest(type: Test) {
    logger.lifecycle("Running consumer contract Tests")
    useJUnitPlatform()
    include "uk/gov/hmcts/fees.register/api/controllers/consumer/**"
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
    systemProperty 'pact.provider.version', project.pactVersion

}

task providerContractTest(type: Test) {
    logger.lifecycle("Running contract provider Tests")
    useJUnitPlatform()
    include "uk/gov/hmcts/fees.register/api/controllers/provider/**"
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
    systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
    systemProperty 'pact.provider.version', project.pactVersion

}

task runProviderPactVerification(type: Test) {
    logger.lifecycle("Runs pact provider Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
    logger.lifecycle("Runs pact consumer Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn consumerContractTest

runAndPublishConsumerPactTests.finalizedBy pactPublish

runProviderPactVerification.dependsOn providerContractTest

runProviderPactVerification.finalizedBy pactVerify

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pitest {
    targetClasses = ['uk.gov.hmcts.fees.register.api.*']
    excludedClasses = ['uk.gov.hmcts.fees.register.api.config.*']
    threads = 10
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
    mutationThreshold = 50
}
