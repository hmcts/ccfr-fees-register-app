description = 'api module'

sourceSets {

    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
    }
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
    }
}

apply plugin: 'idea'
idea {
    module {
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    }
}

def javaLoggingVersion = '2.1.1'

dependencies {
    compile project(':fees-register-model')
    compile project(':fees-register-api-contract')
    compile (group: 'uk.gov.hmcts.auth.checker', name: 'auth-checker-spring', version: '[1.0, )') {
        exclude(module: 'java-logging-spring')
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.9.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '1.5.9.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '1.5.9.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '1.5.9.RELEASE'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile(group: 'org.postgresql', name: 'postgresql', version: '42.1.4') {
        exclude(module: 'commons-logging')
        exclude(module: 'slf4j-simple')
    }
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: javaLoggingVersion
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: javaLoggingVersion

    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.0'

    runtime group: 'org.springframework.boot', name: 'spring-boot-devtools', version: '1.5.9.RELEASE'

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.5.9.RELEASE') {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.2.3.RELEASE'
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.5'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
    testCompile group: 'org.projectlombok', name: 'lombok', version: '1.16.16'

    // smoke
    smokeTestCompile sourceSets.test.runtimeClasspath

    // functional
    functionalTestCompile sourceSets.test.runtimeClasspath
}

test {
    // Base test class for other tests - has no runnable test methods
    exclude '**/ComponentTestBase*'
}

task smokeTest(type: Test, description: 'Runs the smoke tests', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task functionalTest(type: Test, description: 'Runs the smoke functional', group: 'Verification') {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}
